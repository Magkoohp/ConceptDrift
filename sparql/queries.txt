# 0. Clear the graphs

CLEAR GRAPH <http://lod.cedar-project.nl/resource/BRT_1889_08_T1>
CLEAR GRAPH <http://lod.cedar-project.nl/resource/BRT_1899_04_T>

# 1. Dimensions of a table, 1889

PREFIX d2s: <http://www.data2semantics.org/core/>

SELECT DISTINCT ?s
FROM <http://lod.cedar-project.nl/resource/BRT_1889_08_T1>
WHERE {
 ?s a d2s:Dimension .
}

# 2. Dimensions of a table, 1899

PREFIX d2s: <http://www.data2semantics.org/core/>

SELECT DISTINCT ?s 
FROM <http://lod.cedar-project.nl/resource/BRT_1899_04_T>
WHERE {
 ?s a d2s:Dimension .
}

########################
# Dataset construction #
########################

# 3. Population value, 1889

PREFIX d2s: <http://www.data2semantics.org/core/>
PREFIX cd: <http://www.data2semantics.org/data/BRT_1889_08_T1_marked/Tabel_1/>
PREFIX ns1: <http://www.data2semantics.org/core/Tabel_1/> 

SELECT ?cell ?population
FROM <http://lod.cedar-project.nl/resource/BRT_1889_08_T1>
WHERE {
 ?cell d2s:isObservation [ d2s:dimension cd:12___1878 ,
       			   		 cd:M ,
					 cd:O ;
			   ns1:BENAMING_van_de_onderdeelen_der_onderscheidene_beroepsklassen__met_de_daartoe_behoordende_beroepen <http://www.data2semantics.org/data/BRT_1889_08_T1_marked/Tabel_1/Amsterdam/I/b_/Diamantslijpers__incl__verstellers_> ;
			   ns1:Positie_in_het_beroep__aangeduid_met_A__B__C_of_D_ cd:D ;
       			   d2s:populationSize ?population ] .
}

# 4. Ready-to-mine table construction, 1889

PREFIX d2s: <http://www.data2semantics.org/core/>
PREFIX cd: <http://www.data2semantics.org/data/BRT_1889_08_T1_marked/Tabel_1/>
PREFIX ns1: <http://www.data2semantics.org/core/Tabel_1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT (str(?age_s) AS ?age_c) (str(?gender_s) AS ?gender_c) (str(?marital_status_s) AS ?marital_status_c) (str(?municipality_s) AS ?municipality_c) (str(?occ_class_s) AS ?occ_class_c) (str(?occ_subclass_s) AS ?occ_subclass_c) (str(?occupation_s) AS ?occupation_r) (REPLACE(?position_s, "^ +| +$", "") AS ?position_c) ?population
FROM <http://lod.cedar-project.nl/resource/BRT_1889_08_T1>
WHERE {
 ?cell d2s:isObservation [ d2s:dimension ?gender ;
                           d2s:dimension ?marital_status ;
                           d2s:dimension ?age ;
			   ns1:BENAMING_van_de_onderdeelen_der_onderscheidene_beroepsklassen__met_de_daartoe_behoordende_beroepen ?occupation ;
			   ns1:Positie_in_het_beroep__aangeduid_met_A__B__C_of_D_ ?position ;
       			   d2s:populationSize ?population ] .
 ?occupation skos:broader ?occ_subclass .
 ?occ_subclass skos:broader ?occ_class .
 ?occ_class skos:broader ?municipality .
 ?municipality skos:prefLabel ?municipality_s .
 ?occ_class skos:prefLabel ?occ_class_s .
 ?occ_subclass skos:prefLabel ?occ_subclass_s .
 ?cell d2s:cell ?cell_s .
 ?age skos:prefLabel ?age_s .
 ?gender skos:prefLabel ?gender_s .
 ?marital_status skos:prefLabel ?marital_status_s .
 ?occupation skos:prefLabel ?occupation_s .
 ?position skos:prefLabel ?position_s .
 FILTER (?gender IN (cd:M, cd:V))
 FILTER (?marital_status IN (cd:O, cd:G))
 FILTER (?age IN (cd:12___1878, cd:13___1876, cd:14_---_15__1875___---_1874, cd:16_---_17__1873___---_1872, cd:1878_en_later__beneden_12_j_, cd:18_---_22__1871___---_1867, cd:Geboortejaren___leeftijd_in_j_, cd:25_---_35__1864___---_1854, cd:36_---_50__1853___---_1839, cd:51_---_60__1838___---_1829, cd:61_---_65__1828___---_1824, cd:66_---_70__1823_---_1818, cd:71_en_daarboven__1818_en_vroeger, cd:Van_onbekenden_leeftijd, cd:23_---_24__1866___---_1865))
} ORDER BY (?cell)

# 5. Ready-to-mine table construction, 1899


PREFIX d2s: <http://www.data2semantics.org/core/>
PREFIX cd: <http://www.data2semantics.org/data/BRT_1899_04_T_marked/Noordholland/>
PREFIX ns1: <http://www.data2semantics.org/core/Noordholland/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT ?age_s ?gender_s ?marital_status_s ?municipality_s ?occupation_s ?position_s ?population
FROM <http://lod.cedar-project.nl/resource/BRT_1899_04_T>
WHERE {
 ?cell d2s:isObservation [ d2s:dimension ?gender ;
                           d2s:dimension ?marital_status ;
                           d2s:dimension ?age ;
			   ns1:Benaming_van_de_onderdeelen_der_onderscheidene_beroepsklassen__met_de_daartoe_behoorende_beroepen ?occupation ;
                           ns1:Gemeenten ?municipality ;
			   ns1:Positie_in_het_beroep ?position ;
       			   d2s:populationSize ?population ] .
 ?cell d2s:cell ?cell_s .
 ?age skos:prefLabel ?age_s .
 ?gender skos:prefLabel ?gender_s .
 ?marital_status skos:prefLabel ?marital_status_s .
 ?occupation skos:prefLabel ?occupation_s .
 ?position skos:prefLabel ?position_s .
 ?municipality skos:prefLabel ?municipality_s .
 FILTER (?gender IN (cd:MANNEN, cd:VROUWEN))
 FILTER (?marital_status IN (cd:O_, cd:G_))
 FILTER (?age IN (cd:12_of_13_1887_-_1886, cd:14_of_15_1885_-_1884, cd:16_of_17_1883_-_1882, cd:18_-_22_1881_-_1877, cd:23_-_35_1876_-_1864, cd:beneden_12_jaar_1888_en_later, cd:51_-_60_1848_-_1839, cd:61_-_65_1838_-_1834, cd:66_-_70_1833_-_1829, cd:71_en_daarboven_1828_en_vroeger, cd:36_-_50_1863_-_1849))
} ORDER BY (?cell)
